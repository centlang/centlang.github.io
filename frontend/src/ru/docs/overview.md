{% extends docs %}

# Обзор языка

<div class="note">
    Если вы ещё не установили Cent, обратитесь к
    <a href="/ru/docs/install/">Руководству по установке</a> или используйте
    <a href="/ru/play/">Песочницу Cent</a>, чтобы запускать код прямо в
    браузере.
</div>

## Введение

В этом руководстве предполагается, что вы знакомы с базовыми концепциями
программирования и уже использовали другой язык программирования.

## Hello, world!

Мы напишем простую программу, которая выводит `Hello, world!` на экран. Создайте
новый файл с именем `main.cn` со следующим содержимым:

```cent
with std::io;

fn main() {
    io::println("Hello, world!");
}
```

Теперь выполните эти команды, чтобы скомпилировать и запустить программу:

```sh
$ centc main.cn
$ ./main
Hello, world!
```

Вы также можете использовать опцию `--run`, чтобы автоматически выполнить
скомпилированный файл:

```sh
$ centc main.cn --run
Hello, world!
```

## Переменные

Оператор `let` создаёт _неизменяемую_ переменную.

```cent
let language = "Cent";
```

Чтобы создать _изменяемую_ переменную, используйте ключевое слово `mut`.

```cent
mut score = 0;
score = 10;
```

Тип переменной можно указать явно:

```cent
mut score: i32 = 0;
```

Если значение не задано, переменные инициализируются нулём.

```cent
mut score: i32;
```

Cent статически типизирован, поэтому нельзя изменить тип переменной.

```cent
mut score = 10;
score = 4.5; // ошибка!
```

Однако можно _затенить_ переменную, создав новую переменную с тем же именем:

```cent
mut score = 10;
mut score = 4.5;
score = 7.3;
```

```cent
mut score = 10;
let score = score; // score больше не изменяемая
score = 7; // ошибка!
```

## Комментарии

Комментарии помогают объяснять части кода. Они начинаются с `//`.

```cent
let variable = 10; // это переменная
```

## Константы

Для создания констант используйте ключевое слово `const`. Константы вычисляются
на этапе компиляции.

```cent
const PI = 3.14;
const GOLDEN_RATIO = 1.618;
const SECONDS_IN_A_DAY = 60 * 60 * 24;
```

## Типы данных

### Целые числа

Целочисленные типы начинаются с `i` или `u`. Типы, начинающиеся с `u`, —
_беззнаковые_. Далее указывается размер в битах:

```cent
i8 i16 i32 i64 // знаковые
u8 u16 u32 u64 // беззнаковые
```

```cent
let a: u64 = 3;
let b: i8 = -128;
```

Существуют специальные типы `usize` и `isize`. Они имеют размер указателя и
обычно используются для индексации.

### Числа с плавающей запятой

Используются для хранения чисел с плавающей точкой. В Cent есть два таких типа:
`f32` и `f64`.

```cent
let a: f32 = 3.5;
let b: f64 = 1.2345678;
```

### Тип `bool`

Значение `bool` может быть `true` или `false`:

```cent
mut raining: bool = false;
raining = true;
```

### Тип `rune`

Тип `rune` представляет собой символ Unicode и занимает 4 байта.

```cent
let the_letter_h: rune = 'h';
```

### Массивы

Массивы создаются с помощью синтаксиса `[N]T`. Они хранят несколько значений
одного типа:

```cent
let data = [4]u8{0xff, 0xff, 0xff, 0x0};
let data = [_]u8{0xff, 0xff, 0xff, 0x0}; // длина массива может быть выведена
```

Массивы могут иметь _переменную длину_.

<div class="warning">
    Массивы переменной длины размещаются в стеке, размер которого ограничен.
    Если массив слишком большой, это может привести к переполнению стека.
</div>

```cent
mut n: usize = 16;
n = 1024;
mut data: [n]u8;
```

### Срезы

Срезы создаются с помощью синтаксиса `[]T`. Срезы представляют собой вид на
последовательность элементов и состоят из указателя и длины.

```cent
mut data: [1024]u8;
let slice: []u8 = data;

let len = slice.len;
let ptr = &slice[0];
```

Срезы могут быть изменяемыми:

```cent
mut data: [1024]u8;
let slice: []mut u8 = data;

slice[10] = 42;
```

### Строки

В Cent строки — это просто массивы байт. Строки **не** оканчиваются нулём.

```cent
let language: [4]u8 = "Cent";
let language = [_]u8{'C' as u8, 'e' as u8, 'n' as u8, 't' as u8};

let null_terminated = "Hello, world!\0";
```

### Опциональные типы

Опциональные значения могут быть `null` или содержать значение. Для создания
опционального типа используйте `?T`:

```cent
mut optional: ?i32 = 32; // optional != null

optional = 42;
optional = null; // optional == null
```

Чтобы получить значение без проверки, используйте синтаксис `.!`:

```cent
mut optional: ?i32 = 32;
let value = optional.!;
```

### Указатели

Указатели ссылаются на значение в памяти. Для создания указателя используется
синтаксис `*T`.

```cent
mut x = 42;
let ptr: *i32 = &x; // *ptr = 42
x = 422; // *ptr = 422
```

Указатели могут быть изменяемыми:

```cent
mut x = 42;
let ptr: *mut i32 = &x;
*ptr = 422; // x = 422
```

<div class="note">
    В Cent указатели не могут быть <code>null</code>. Если нужен такой указатель, используйте <em>опциональный указатель</em>. Опциональный указатель имеет тот же размер, что и обычный.
</div>

### Кортежи

Кортежи создаются с помощью `(T1, T2, T3, ...)`. Они хранят несколько значений
разных типов:

```cent
mut data: (i32, f32, bool, [6]u8) = (10, 42.42, true, "Hello!");
data.0 += 32; // data.0 = 42
data.1 = data.0; // data.1 = 42
```

## Оператор `with`

С помощью `with` можно импортировать модули.

```cent
with std::io;
with std::fs;

with std::posix as os; // импорт под другим именем
```

## Функции

Функции определяются с помощью ключевого слова `fn`. Функция `main` является
входной точкой. Функции можно вызывать до их определения.

```cent
with std::io;

fn main() {
    hello_world();
}

fn hello_world() {
    io::println("Hello, world!");
}
```

Можно указать тип возвращаемого значения после скобок. Если тип не указан,
функция ничего не возвращает. Для возврата значения используется `return`:

```cent
fn get_magic_number() i32 {
    return 42;
}
```

Функции могут принимать параметры:

```cent
fn main() {
    let a = add(3, 4); // a = 7
}

fn add(a: i32, b: i32) i32 {
    return a + b;
}
```

### Параметры по умолчанию

Можно задавать _параметры по умолчанию_. Если аргументы не переданы,
используются значения по умолчанию.

```cent
fn main() {
    let ten = add(3, 7);
    let nine = add(3, 3, 3);
    let one = add(1, -1, 1, 0);
}

fn add(a: i32, b: i32, c: i32 = 0, d: i32 = 0) i32 {
    return a + b + c + d;
}
```

## Управляющие конструкции

### `if` выражения

`if` используется для ветвления кода в зависимости от условия.

```cent
with std::io;

fn main() {
    print_is_even(3); // x нечётный
    print_is_even(4); // x чётный
}

fn print_is_even(x: i32) {
    if x % 2 == 0 {
        io::println("x чётный!");
    } else {
        io::println("x нечётный!");
    }
}
```

Можно использовать `else if` для проверки дополнительных условий.

```cent
with std::io;

fn greet(hour: u8) {
    if hour < 12 {
        io::println("Доброе утро!");
    } else if hour < 18 {
        io::println("Здравствуйте!");
    } else {
        io::println("Добрый вечер!");
    }
}
```

### `switch` выражения

`switch` позволяет сравнивать значение с несколькими возможными вариантами:

```cent
with std::io;

fn day_of_week(day: u8) {
    switch day {
        1 { io::println("Понедельник"); }
        2 { io::println("Вторник"); }
        3 { io::println("Среда"); }
        4 { io::println("Четверг"); }
        5 { io::println("Пятница"); }
        6 { io::println("Суббота"); }
        7 { io::println("Воскресенье"); }
        else { io::println("Неверный день недели!"); }
    }
}
```

Можно объединять несколько значений в одном "кейсе":

```cent
with std::io;

fn is_weekend(day: u8) {
    switch day {
        1, 2, 3, 4, 5 { io::println("Будний день"); }
        6, 7 { io::println("Выходной!"); }
    }
}
```

### Цикл `while`

Цикл выполняется, пока условие `true`.

```cent
with std::io;

fn main() {
    mut i = 0;

    while i < 10 {
        i += 1;
    }

    io::print_int(i); // 10
    io::print_rune('\n');
}
```

Для бесконечного цикла используйте `while true`. Для выхода из цикла
используется ключевое слово `break`.

```cent
with std::io;

fn main() {
    mut i = 0;

    while true {
        if i == 100 {
            break;
        }

        i += 2;
    }

    io::print_int(i); // 100
    io::print_rune('\n');
}
```

Для пропуска итерации используйте `continue`:

```cent
with std::io;

fn main() {
    mut i = 0;
    mut sum = 0;

    while i < 10 {
        i += 1;

        if i % 2 == 0 {
            continue;
        }

        sum += i;
    }

    io::print_int(sum); // 25
    io::print_rune('\n');
}
```

### Цикл `for`

`for` позволяет итерировать диапазоны или последовательности.

_Исключающий диапазон_ создаётся с помощью `x..y`:

```cent
with std::io;

fn main() {
    for i in 1..10 {
        io::print_int(i);
        io::print_rune('\n');
    }
}
```

_Включающий диапазон_ создаётся с помощью `x..=y`:

```cent
with std::io;

fn main() {
    for i in 1..=10 {
        io::print_int(i);
        io::print_rune('\n');
    }
}
```

## Выражения

### Бинарные выражения

| Оператор                    | Приоритет |
| --------------------------- | --------- |
| `*` `/` `%`                 | 1         |
| `+` `-`                     | 2         |
| `<<` `>>`                   | 3         |
| `<` `>` `==` `!=` `>=` `<=` | 4         |
| `&`                         | 5         |
| `^`                         | 6         |
| `\|`                        | 7         |
| `&&`                        | 8         |
| `\|\|`                      | 9         |
| `??`                        | 10        |

```cent
let a = 2 + 3 * 6; // a = 20
```

### Унарные выражения

```cent
let a = 5;
let b = !true; // b = false
mut c = -a; // c = -5
let p = &c;
*p = -c; // c = 5
```
